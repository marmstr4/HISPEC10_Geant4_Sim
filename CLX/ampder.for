	SUBROUTINE A (R_AMPDOT,I_AMPDOT,R_AMP,I_AMP,MRANGE,CAT,ZETA,
	1LMAX,ISMAX,MRMAX,NZMAX)
C
C	COMPUTE THE AMPDOTS
C
c       modified by Th Kroell:
c       12.10.94      maximum numbers of states increased

        parameter (nstatmax=100)       
	INTEGER SSTART(NSTATMAX),S_HALT(NSTATMAX)
	REAL*8 R_AMPDOT(2000,LMAX),I_AMPDOT(2000,LMAX)
	REAL*8 R_AMP(2000,LMAX),I_AMP(2000,LMAX),R_RC,I_RC,RRC,IRC
	REAL R_QLM(5,5),I_QLM(5,5),I_EX
	DIMENSION ZETA(NZMAX),MRANGE(2,MRMAX),LDNUM(5,NSTATMAX)
        dimension LEAD(NSTATMAX,20,5)
	DIMENSION LAMDA(5),ETAN(NSTATMAX),CAT(ISMAX)
	COMMON /AMP/NMAX,SSTART,S_HALT,LEAD,LDNUM,ETAN
	COMMON /QLM/W,EPS,EROOT,LAMMAX,LAMDA,R_QLM,I_QLM,RALFA
C
	NZ=0
	MR=0
	DO IR=1,ISMAX
	  DO L=1,LMAX
	    R_AMPDOT(IR,L)=0.
	    I_AMPDOT(IR,L)=0.
	  END DO
	END DO
	DO I1=1,LAMMAX
	  IF ( LAMDA(I1) .NE. 5 ) THEN
	  DO N=1,NMAX
	    DO I2=1,LDNUM(I1,N)
	      M=LEAD(N,I2,I1)
	      XI=ETAN(N)-ETAN(M)
	      R_EX=COS(XI*RALFA)
	      I_EX=SIN(XI*RALFA)
	      DO IR=SSTART(N),S_HALT(N)
	        MR=MR+1
		DO I3=1,MRANGE(1,MR)
	          IS=MRANGE(2,MR)+I3
		  NZ=NZ+1
		  MUA=INT(ABS(CAT(IS)-CAT(IR))+1.01)
		  R_RC=(R_QLM(I1,MUA)*R_EX-I_QLM(I1,MUA)*I_EX)*ZETA(NZ)
		  I_RC=(R_QLM(I1,MUA)*I_EX+I_QLM(I1,MUA)*R_EX)*ZETA(NZ)
		  DO L=1,LMAX
		    RRC=R_RC*R_AMP(IS,L)-I_RC*I_AMP(IS,L)
		    IRC=R_RC*I_AMP(IS,L)+I_RC*R_AMP(IS,L)
		    R_AMPDOT(IR,L)=R_AMPDOT(IR,L)+IRC
		    I_AMPDOT(IR,L)=I_AMPDOT(IR,L)-RRC
		  END DO
		END DO
	      END DO
	    END DO
	  END DO
	  ELSE
	  DO N=1,NMAX
	    DO I2=1,LDNUM(I1,N)
	      M=LEAD(N,I2,I1)
	      XI=ETAN(N)-ETAN(M)
	      R_EX=COS(XI*RALFA)
	      I_EX=SIN(XI*RALFA)
	      DO IR=SSTART(N),S_HALT(N)
	        MR=MR+1
		DO I3=1,MRANGE(1,MR)
		  IS=MRANGE(2,MR)+I3
		  NZ=NZ+1
		  MU=INT(CAT(IS)-CAT(IR)+.01)
		  MUA=ABS(MU)+1
		  R_RC=(R_QLM(I1,MUA)*R_EX-I_QLM(I1,MUA)*I_EX)*ZETA(NZ)
		  I_RC=(R_QLM(I1,MUA)*I_EX+I_QLM(I1,MUA)*R_EX)*ZETA(NZ)
		  IF ( MU .LT. 0 ) THEN
		   R_RC=-R_RC
		   I_RC=-I_RC
		  END IF
		  DO L=1,LMAX
		    RRC=R_RC*R_AMP(IS,L)-I_RC*I_AMP(IS,L)
		    IRC=R_RC*I_AMP(IS,L)+I_RC*R_AMP(IS,L)
		    R_AMPDOT(IR,L)=R_AMPDOT(IR,L)+IRC
		    I_AMPDOT(IR,L)=I_AMPDOT(IR,L)-RRC
		  END DO
		END DO
	      END DO
	    END DO
	  END DO
	  END IF
	END DO
	RETURN
	END
